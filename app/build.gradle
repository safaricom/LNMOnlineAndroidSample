/*
 *
 *  * Copyright (C) 2017 Safaricom, Ltd.
 *  *
 *  * Licensed under the Apache License, Version 2.0 (the "License");
 *  * you may not use this file except in compliance with the License.
 *  * You may obtain a copy of the License at
 *  *
 *  * http://www.apache.org/licenses/LICENSE-2.0
 *  *
 *  * Unless required by applicable law or agreed to in writing, software
 *  * distributed under the License is distributed on an "AS IS" BASIS,
 *  * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  * See the License for the specific language governing permissions and
 *  * limitations under the License.
 *
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.myduka.app"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    buildTypes.each {

        // CONSUMER_KEY & CONSUMER_SECRET Build Variables are referenced from gradle.properties file
        it.buildConfigField 'String', 'CONSUMER_KEY', DARAJA_CONSUMER_KEY
        it.buildConfigField 'String', 'CONSUMER_SECRET', DARAJA_CONSUMER_SECRET
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.support.appcompat
    implementation deps.support.design
    implementation deps.support.support
    implementation deps.support.multidex
    implementation deps.support.cardview
    implementation deps.support.recyclerview
    implementation deps.support.constraintLayout
    //Butterknife - Bind Views
    implementation deps.butterknife.core
    annotationProcessor deps.butterknife.compiler
    //Log - Replace the Android Log Class
    implementation deps.timber
    //Firebase
    implementation deps.firebase
    //My Lib -> Monitor Internet Connectivity
    implementation deps.networkmanager
    //Glide - Handle Images
    implementation deps.glide.core
    annotationProcessor deps.glide.compiler
    //Sweet Alert Replacement
    implementation deps.sweetalert
    //Retrofit - Network Client
    implementation deps.retrofit.core
    implementation deps.retrofit.gsonConverter
    //OKHTTP3
    implementation deps.okhttp3.core
    implementation deps.okhttp3.interceptor
    //GSON
    implementation deps.gson
    //OKIO
    implementation deps.okio

    //Testing !!!
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

}
apply plugin: 'com.google.gms.google-services'
